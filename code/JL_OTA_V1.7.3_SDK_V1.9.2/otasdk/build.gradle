apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: "kotlin-kapt"
android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.jieli.otasdk"
        minSdkVersion 21
        targetSdk 29
        versionCode 10707
        versionName "1.7.3"
        archivesBaseName = "JLOTA_V${versionName}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildToolsVersion = '30.0.3'

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    dataBinding {
        enabled = true
    }
    buildFeatures {
        viewBinding true
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release" || variant.buildType.name == "debug") {
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name != null) {
                    def suffix = ""
                    if (outputFile.name.endsWith('.apk')) {
                        suffix = ".apk"
                    } else if (outputFile.name.endsWith('.aab')) {
                        suffix = ".aab"
                    }
                    if(suffix != ""){
                        def fileName = "JLOTA-V${versionName}-${variant.buildType.name}_${getAPKSuffix()}$suffix"
                        if (variant.buildType.name == "release") {
                            fileName = "JLOTA-V${versionName}$suffix"
                        }
                        outputFileName = fileName
                    }
                }
            }
        }
    }
}
static def getSystemTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00"))
}

static def getAPKSuffix() {
    return "${getSystemTime()}_1"
}
repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(dir: 'libs', include: ['*.aar'])

//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'

    implementation 'com.kyleduo.switchbutton:library:2.0.0'
    //RecyclerView通用适配器模板库
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'

    //权限库
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:4.9.2"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2"

    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'com.anggrayudi:storage:0.13.0'
    //自定义seekbar
    implementation 'com.github.warkiz.widget:indicatorseekbar:2.1.2'
    //事件总线 封装的RXJava
    implementation("com.hwangjr.rxbus:rxbus:1.0.5") {
        exclude group: 'com.jakewharton.timber', module: 'timber'
    }
    //局域网快传框架
    implementation "com.koushikdutta.async:androidasync:3.1.0"
    //日志打印工具
    implementation 'com.jakewharton.timber:timber:4.1.2'
    //过渡动画库
    implementation 'com.wang.avi:library:2.1.3'
    //json解析库
    implementation 'com.google.code.gson:gson:2.10'
    //网络库
    implementation("com.squareup.okhttp3:okhttp-bom:4.9.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.0")
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    implementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
}
